<!-- MHonArc v2.6.18 -->
<!--X-Subject: Re: [ferret_users] multiple legend -->
<!--X-From-R13: "Oafyrl Q. [naxr" <nafyrl.o.znaxrNabnn.tbi> -->
<!--X-Date: Tue, 6 Aug 2019 09:47:21 &#45;0700 -->
<!--X-Message-Id: f62c8849&#45;3091&#45;7ed7&#45;2a4b&#45;749f4f75abca@noaa.gov -->
<!--X-Content-Type: multipart/mixed -->
<!--X-Reference: CAMvWTtV314HNz8xJcGaLH5Nv4CmSo5BDdkXR5h+uM=KZ5kg=rA@mail.gmail.com -->
<!--X-Head-End-->
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN"
        "http://www.w3.org/TR/html4/loose.dtd">
<html>
<head>
<title>Re: [ferret_users] multiple legend</title>
</head>
<body>
<!--X-Body-Begin-->
<!--X-User-Header-->
<!--X-User-Header-End-->
<!--X-TopPNI-->
<HR>
<center>[<a href="msg00316.html">Thread Prev</a>][<a href="msg00318.html">Thread Next</a>][<A HREF="threads.html#00317">Index</A>]</center>

<!--X-TopPNI-End-->
<!--X-MsgBody-->
<!--X-Subject-Header-Begin-->
<h1>Re: [ferret_users] multiple legend</h1>
<hr>
<!--X-Subject-Header-End-->
<!--X-Head-of-Message-->
<ul>
<li><strong>To</strong>: <strong><a href="mailto:ferret_users@DOMAIN.HIDDEN">ferret_users@xxxxxxxx</a></strong></li>
<li><strong>Subject</strong>: <strong>Re: [ferret_users] multiple legend</strong></li>
<li><strong>From</strong>: <strong>&quot;Ansley C. Manke&quot; &lt;<a href="mailto:ansley.b.manke@DOMAIN.HIDDEN">ansley.b.manke@xxxxxxxx</a>&gt;</strong></li>
<li>Date: Tue, 6 Aug 2019 09:47:13 -0700</li>
<li>Arc-authentication-results: i=2; mx.google.com;       dkim=pass header.i=@noaa.gov header.s=google header.b=d04Gr30k;       spf=pass (google.com: domain of <a href="mailto:ansley.b.manke@DOMAIN.HIDDEN">ansley.b.manke@xxxxxxxx</a> designates 209.85.220.41 as permitted sender) smtp.mailfrom=<a href="mailto:ansley.b.manke@DOMAIN.HIDDEN">ansley.b.manke@xxxxxxxx</a>;       dmarc=pass (p=REJECT sp=NONE dis=NONE) header.from=noaa.gov</li>
<li>Arc-authentication-results: i=1; mx.google.com;       dkim=pass header.i=@noaa.gov header.s=google header.b=d04Gr30k;       spf=pass (google.com: domain of <a href="mailto:ansley.b.manke@DOMAIN.HIDDEN">ansley.b.manke@xxxxxxxx</a> designates 209.85.220.41 as permitted sender) smtp.mailfrom=<a href="mailto:ansley.b.manke@DOMAIN.HIDDEN">ansley.b.manke@xxxxxxxx</a>;       dmarc=pass (p=REJECT sp=NONE dis=NONE) header.from=noaa.gov</li>
<li>Arc-message-signature: i=2; a=rsa-sha256; c=relaxed/relaxed; d=google.com; s=arc-20160816;        h=list-archive:list-help:list-post:list-id:mailing-list:precedence         :content-language:in-reply-to:mime-version:user-agent:date         :message-id:from:references:to:subject:dkim-signature;        bh=5Ku9+2raW7uJJA5KelrfeN1oqk5OcouMcPf+QRW4Yfc=;        b=OvcM//gonT910fgdmNNUxYA134vBE2x2PoXbDfPu9onGdDosWaBerjcInxWgzZMUHL         Swi5FgwIgiyHhL2XG7IB55147cjh8PznMI1Hb1fefAJZoKZXXP3kJ6wsXUSi29xA2Exr         7OFk+JQqbSpw9KyclQGJ7Oenjr5TAls5aJQTCzEXs8gbQ98bHL09hxWHEN9srxICoPUR         b61LoXaHDimd3IyECJnsfL9zCtQ0JAXcaKmLBmPVHqY8RnEPLUQPcKpHNRr14njY4j5k         Tag2XGYari46KCH86lN9YjMTPVdN+RMXpJxW5+Cop9hWcZ0K+nF3LD6jYm98pCH/20bs         6Syw==</li>
<li>Arc-message-signature: i=1; a=rsa-sha256; c=relaxed/relaxed; d=google.com; s=arc-20160816;        h=content-language:in-reply-to:mime-version:user-agent:date         :message-id:from:references:to:subject:dkim-signature;        bh=5Ku9+2raW7uJJA5KelrfeN1oqk5OcouMcPf+QRW4Yfc=;        b=0A47QB1lAf0EoJcL26yxPOR29hQOdBeX3H2K83RYcsCyHyo1ywqeBjnIyE4EK+aLFE         wg8w3n8qEDHyrhzrZK3N+orqI1gbmr/Rynk6GuwOrFPbMUvc1WSV1PQKxegPpHcR5h37         zJavEA7GF///3qGBjJss6LSQA5tDWMZmJxigoas9RFCExkSi/uNCMxDlajXDK+ORmK2n         KkjIMriibtdDZehc4OjXkq/sat/Ftr5FLFDKiMPCehsy6JyYsiPi3mdEIJgIyiAocWTS         5iW0nqMzOWJtdCtf8pjZ/+vsfxIIctKmw+ZpPeHcf+3DeTQEIaWi+M80Fo7I+h8q0N+w         omwQ==</li>
<li>Arc-seal: i=2; a=rsa-sha256; t=1565110040; cv=pass;        d=google.com; s=arc-20160816;        b=kAxbJqmzxscRJf7iLFfeo4BSsS2wdG1QMYwHM9IbSiBkQCUUGmB0+C7tUuPVKedubl         V68Qg1IZPTd6yFm0q80nsL+wdb//51YdwLw/o357yoWfcJ3mn8pCMV/pwbnZtKSCzBl9         rnCPK551k2jIkWg/0Jbhozhg1DV3V73WuUWiFM8FxUZoAzM0VWF9sgvUMFWFhw0oC3O4         w++1iLRY1viQbJebGdu25JpDWUj0SoyIZZO0DrzLMBNiQwDgxwtkxi5zhEwR4r3tNpSY         uS0BDO321HgfwRXb+v9XGYkOtW3RU6RUBybcgq8Li20hPd7sh2hj5lj6W1YIn5Ae9F/3         LDNA==</li>
<li>Arc-seal: i=1; a=rsa-sha256; t=1565110039; cv=none;        d=google.com; s=arc-20160816;        b=QiYQ8zJIzLQwkqr4yx8gJALjCg8EeyIx8EiFqMgoreVWARXAHvS0OQ17JZUIIKiW9W         j2nWcfvmeLizswSueTSTzeGUP4vEJgEVvt75dztINyb1G9z85xIVoJR5cbLG9SQZNu0+         OqPg6/kwN1nNSP7zdNG57FLqFKTmrcYOa1O51TzkKyxzcAzcU6VlF5pSp/dUn5aElcgD         AV0vigbFC6AQLB7RpR3kOkovww02lEn+6bc/QIoWsybUchcm84WYR4TCJvWZIyqCG+YK         mlvUG+ragpKx6R3rwYPE3czw/Vl8XzjOKauDattLbPab1kwg+YC1kurjZX00TIiMBAY5         fViw==</li>
<li>Dkim-signature: v=1; a=rsa-sha256; c=relaxed/relaxed;        d=noaa.gov; s=google;        h=subject:to:references:from:message-id:date:user-agent:mime-version         :in-reply-to:content-language:x-original-sender         :x-original-authentication-results:precedence:mailing-list:list-id         :list-post:list-help:list-archive;        bh=5Ku9+2raW7uJJA5KelrfeN1oqk5OcouMcPf+QRW4Yfc=;        b=UADQPSGAyYyNm9ihr+L8gVBPicNZuhVokft4+H98sTXOsPYXDmP1A0ypozuH12ERrV         WmKeIBKUueGexcmYnkPU3C7mLY/rndK//pW4NtrCwPI8oPK15BXJPqu4lzCaApG1NcI+         sbSYtdFKsqhXrD8DlYSuGQhUdGLzDY9iqmSvMkIVZvpS0uVm+DeaYfZQDiLYo+QJcGFP         WPpwj5gx1ESckAr1wU3FqgL8D9grXQCOoxYA2gVJZgk9uE0gytW9EWnD1wbCFhAYDEOI         XNhDrPhg3gJE+ap1kxWxZMbpXaUmcF08JBzuQKtNvzO70UfVkBW4aTUCJ+GJ1UmdOVWy         kLnQ==</li>
<li>In-reply-to: &lt;CAMvWTtV314HNz8xJcGaLH5Nv4CmSo5BDdkXR5h+uM=KZ5kg=rA@mail.gmail.com&gt;</li>
<li>List-archive: &lt;<a href="https://groups.google.com/a/noaa.gov/group/ferret_users/">https://groups.google.com/a/noaa.gov/group/ferret_users/</a>&gt;</li>
<li>List-help: &lt;<a href="https://support.google.com/a/noaa.gov/bin/topic.py?topic=25838">https://support.google.com/a/noaa.gov/bin/topic.py?topic=25838</a>&gt;, &lt;<a href="mailto:ferret_users+help@noaa.gov">mailto:ferret_users+help@noaa.gov</a>&gt;</li>
<li>List-id: &lt;ferret_users.noaa.gov&gt;</li>
<li>List-post: &lt;<a href="https://groups.google.com/a/noaa.gov/group/ferret_users/post">https://groups.google.com/a/noaa.gov/group/ferret_users/post</a>&gt;, &lt;<a href="mailto:ferret_users@noaa.gov">mailto:ferret_users@noaa.gov</a>&gt;</li>
<li>Mailing-list: list <a href="mailto:ferret_users@DOMAIN.HIDDEN">ferret_users@xxxxxxxx</a>; contact <a href="mailto:ferret_users%2Bowners@DOMAIN.HIDDEN">ferret_users+owners@xxxxxxxx</a></li>
<li>References: &lt;CAMvWTtV314HNz8xJcGaLH5Nv4CmSo5BDdkXR5h+uM=KZ5kg=rA@mail.gmail.com&gt;</li>
<li>Sender: <a href="mailto:owner-ferret_users@DOMAIN.HIDDEN">owner-ferret_users@xxxxxxxx</a></li>
<li>User-agent: Mozilla/5.0 (Windows NT 6.1; WOW64; rv:60.0) Gecko/20100101 Thunderbird/60.8.0</li>
</ul>
<!--X-Head-of-Message-End-->
<!--X-Head-Body-Sep-Begin-->
<hr>
<!--X-Head-Body-Sep-End-->
<!--X-Body-of-Message-->
<table width="100%"><tr><td bgcolor="#FFFFFF" style="background-color: #FFFFFF; color: #000000; "><font color="#000000">
  
  
    <p>Hi,</p>
    <p>The command qualifier PLOT/KEY=title will use the titles from the
      variables in the legend of a line plot. Try this example</p>
    <p>yes? let/title="Line one" _one_ =&#xA0; 3*X+2<br>
      yes? let/title= "Line Two" two = X - 1.5<br>
      yes? let/title = "Line Three" three =&#xA0; 4*X+3<br>
      yes? plot/color/key=title/x=1:100 one, two, three<br>
    </p>
    <p>(I also used PLOT/COLOR there.&#xA0; This is a fairly recent addition;
      PLOT/COLOR with no argument will skip black as a color and start
      with the colored lines.)</p>
    <p>For your script, the problem is that you can't nest grave-accent
      expressions, but you can have a symbol evaluation inside a
      grave-accent _expression_. Something along these lines should work:<br>
    </p>
    <div>!MAIN: PLOTTING (2)</div>
    <div>plot/thick=
      1/vlim=25:31:0.2 /color=19 a19<br>
      repeat/name=mn/range=1:18 (\<br>
      plot/over/thick=1/vlim=25:31:0.2/symbol=`mn`/line=`mn` a`mn`;\</div>
    <div>define symbol mn= `mn`;\ <br>
    </div>
    <div>go legend `mn` "`a($mn),return=title`" ur;\<br>
      ppl plot;\<br>
      )</div>
    <p>There is yet another option, a new script linekey.jnl, which was
      just added and is not yet in the PyFerret/Ferret releases. It was
      mentioned here,
<a rel="nofollow" class="moz-txt-link-freetext" href="https://www.pmel.noaa.gov/maillists/tmap/ferret_users/fu_2019/msg00183.html">https://www.pmel.noaa.gov/maillists/tmap/ferret_users/fu_2019/msg00183.html</a>.
      I have attached linekey.jnl and test_linekey.jnl which is a demo
      script that calls it.&#xA0; This lets you put the legend inside the
      plot box or outside its edges, and gives control over symbols used
      in the line samples.</p>
    <p>Ansley<br>
    </p>
    <div class="moz-cite-prefix">On 8/5/2019 6:51 PM, Saat Mubarrok
      wrote:<br>
    </div>
    <blockquote type="cite"
cite="">
      
      <div dir="ltr">
        <div>Dear community,</div>
        <div><br>
        </div>
        <div>I was trying to plot multiple line for seasonal variation
          from 19 models from different data sets.</div>
        <div>But the problem is I cannot put the legend using
          legend.jnl. I was trying to use two option based on FAQ and
          resource from internet:</div>
        <div><br>
        </div>
        <div>...<br>
        </div>
        <div> !MAIN:CALCULATE SEASONAL VARIATION<br>
          repeat/name=mm/range=1:19 (\<br>
          let modelid=model[i=`mm`];\<br>
          let/title="`modelid`"
          a`mm`=thetao_sst[d=`mm`,y=5s:10s@ave,x=50e:80e@ave, z=1,
          gt=month_reg@mod];\<br>
          )</div>
        <div><br>
        </div>
        <div>!MAIN:PLOTTING (1)<br>
          plot/thick=1/vlim=25:31:0.2
          a1,a2,a3,a4,a5,a6,a7,a8,a9,a10,a11,a12,a13,a14,a15,a16,a17,a18,a19;\</div>
        <div>....</div>
        <div><br>
        </div>
        <div>The problem from script above is the legend placed at below
          the figure and the name of each line is not the model name
          (modelid) but variable `a1,a2,a3..` instead. I want to put the
          legend in upper right of figure.<br>
        </div>
        <div>...<br>
        </div>
        <div>!MAIN: PLOTTING (2)</div>
        <div>plot/thick=
          1/vlim=25:31:0.2 /color=19 a19<br>
          repeat/name=mn/range=1:18 (\<br>
          plot/over/thick=1/vlim=25:31:0.2/symbol=`mn`/line=`mn` a`mn`;\<br>
          go legend `mn` "`a`mn`,return=title`" ur;\<br>
          ppl plot;\<br>
          )</div>
        <div>...</div>
        <div><br>
        </div>
        <div>The problem is that: <br>
        </div>
        <div>**ERROR: variable unknown or not in data set: A<br>
          go legend 1 "`a`mn`,return=title`" ur<br>
          Command file, command group, or REPEAT execution aborted</div>
        <div><br>
        </div>
        <div>So my purpose is to put the legend in upper right part and
          has a model name on it.</div>
        <div>Because I am new to ferret, so any other help, lead, and
          suggestion is much appreciate. <br>
        </div>
        <div>Thank you very much.<br>
        </div>
        <div><br>
        </div>
        <div>NOAA/PMEL TMAP<br>
          &#xA0; FERRET v6.64 &#xA0;<br>
          &#xA0; Linux(gfortran) 2.6.9-89.0.20.ELsmp - 09/16/10<br>
          &#xA0; 6-Aug-19 10:50</div>
        <div><br>
        </div>
        <div><br>
        </div>
        <div>Saat M.</div>
        <div>KIOST<br>
        </div>
        <div><br>
        </div>
        <div><br>
        </div>
        <div><br>
        </div>
      </div>
    </blockquote>
    <div class="moz-signature">-- <br>
      <i>Ansley Manke<br>
        NOAA/PMEL Science Data Integration Group<br>
        7600 Sand Point Way NE<br>
        206-526-6246</i></div>
  

</font></td></tr></table><pre>\cancel mode verify
!
! linekey.jnl
!   Draw a labeled line sample on a plot, a single line in a legend.
!   User-specified PLOT command with qualifiers for line style, color, 
!     symbol, thickness.
! 
! 5/22/2019
! 
! Arguments
!   1  line number
!   2  title
!   3  plot command with thickness, color, symbol, size
!   4  location on the page ul ur ll lr ulc urc llc lrc, where:
!        ul, ur, ll, lr     are upper left, upper right etc, within the plot box
!        ulc, urc, llc, lrc are upper left corner, etc outside the plot box.
! 
!  For legends in the plot corners, you may need to increase the margins for them to fit.
!  See the margins script:  yes? GO/HELP margins.jnl
!  To make the legend in the upper right of a page, use &quot;CANCEL MODE LOGO&quot; 
!  to remove the ferret-version and date lines.
!
! Example:
!   cancel mode logo
!   go margins 1.4  1.4  1.2  1.2
!
!   plot/i=1:60/thick/sym=20 cos(i/10)
!   plot/over/nolab/i=1:60/thick/size=0.16/line/sym=30/color=red sin(i/10)
!   go linekey 1 &quot;cosine&quot; plot/thick/sym=20/color=black ur
!   go linekey 2 &quot;sine&quot; plot/thick/size=0.16/line/sym=30/color=red ur
!
! NOTE:  For time plots, when running PyFerret, if you have issued SET TEXT for system fonts
!        this script issues a CANCEL TEXT because the text is mis-located using SET TEXT.
!
! For the legends drawn within the plot box, the first line is drawn nearest
! the upper or lower axis line of the plot, with each new line drawn below that 
! for upper-located legends or above for lower-located ones.

! For legends drawn outside the plot box, the first line is drawn nearest the plot
! box. For upper-located legends the first line is drawn just above the upper axis
! with more lines added above that. For lower-located legends the first line is drawn
! just below the bottom axis with more lines added below that.

DEFINE SYMBOL lk_lnum = ($1)
DEFINE SYMBOL lk_title = ($2%&quot; &quot;%)
DEFINE SYMBOL plot_command = `upcase(&quot;($3)&quot;)`
DEFINE SYMBOL lk_where = ($4)
DEFINE SYMBOL lk_lsize = ($5&quot;0&quot;)

query/ignore $4%1|ul&gt;1|ur&gt;1|ll&gt;1|lr&gt;1|ulc&gt;1|urc&gt;1|llc&gt;1|lrc&gt;1|\
 &lt;location must be ul, ur, ll, lr, ulc, urc, llc or lrc%

! currently SET TEXT setting mis-positions the text on some plots (time-axis plots?)
IF `($program_name&quot;0|PyFerret&gt;1|*&gt;0&quot;) and ($AX_HORIZ%0|T&gt;1|*&gt;0%) or ($AX_VERT%0|T&gt;1|*&gt;0%)` THEN 
   CANCEL TEXT
ENDIF

IF `substring(&quot;($plot_command)&quot;, 1,1) NE &quot;P&quot;` THEN 
  SAY Argument 3 must be a PLOT command, e.g. PLOT/COLOR=RED
  EXIT/SCRIPT
ENDIF
DEFINE SYMBOL plot_command = ($plot_command)/NOLAB/OVER/VS

IF `($lk_lsize) EQ 0` THEN DEFINE SYMBOL lk_lsize = 0.11


! IF it's not explicitly plot/sym=  THEN add /line.  By default plot/vs makes symbol plots
IF `STRINDEX(&quot;($plot_command)&quot;, &quot;SY&quot;) EQ 0` THEN DEFINE SYMBOL plot_command = ($plot_command)/line


LET lk_xrange = ($XAXIS_MAX)-(($XAXIS_MIN))
LET lk_yrange = ($YAXIS_MAX)-(($YAXIS_MIN))

LET lk_aspect = ($PPL$HEIGHT)/($PPL$WIDTH)
LET lk_xdel = 0.04* lk_aspect
LET lk_ydel = 0.03/ lk_aspect


! expand things a bit IF we're in a viewport
LET lk_xdel = `lk_xdel/(($vp_xhi%1%)-($vp_xlo%0%))`
LET lk_ydel = `lk_ydel/(($vp_yhi%1%)-($vp_ylo%0%))`

IF `($lk_where%1|ul&gt;1|*&gt;0%) EQ 1` THEN     ! left top

   LET lk_x1pt = (0.5*lk_xdel)* lk_xrange + (($XAXIS_MIN))
   LET lk_x2pt = lk_xdel*lk_xrange + lk_x1pt
   
   LET lk_yfac = (1-0.5*lk_ydel) - (($1)-1)* lk_ydel
   LET lk_y1pt = lk_yfac * lk_yrange + ($YAXIS_MIN)
   
   ($plot_command) {`lk_x1pt`,`lk_x2pt`}, {`lk_y1pt`, `lk_y1pt`}
   
   LET lk_xlabloc = lk_x2pt + 0.02*lk_xrange
   IF ($AX_HORIZ%0|T&gt;1|*&gt;0%) THEN LET lk_xlabloc = ($PPL$XMAX1)+ 0.02*LK_XRANGE
   
   ANNOTATE/XPOS=`lk_xlabloc`/YPOS=`lk_y1pt`/HALIGN=-1/VALIGN=0/SIZE=($lk_lsize) &quot;($lk_title)&quot;

ELIF `($lk_where%1|ur&gt;1|*&gt;0%) EQ 1` THEN   ! right top

   LET lk_x1pt = (1-1.5*lk_xdel)* lk_xrange + (($XAXIS_MIN))
   LET lk_x2pt = lk_xdel*lk_xrange + lk_x1pt
   
   LET lk_yfac = (1-0.5*lk_ydel) - (($1)-1)* lk_ydel
   LET lk_y1pt = lk_yfac * lk_yrange + ($YAXIS_MIN)

   LET lk_xlabloc = lk_x1pt - 0.02*lk_xrange

   ($plot_command) {`lk_x1pt`,`lk_x2pt`}, {`lk_y1pt`, `lk_y1pt`}
   
   LET lk_xlabloc = lk_x1pt - 0.02*lk_xrange
   IF ($AX_HORIZ%0|T&gt;1|*&gt;0%) THEN LET lk_xlabloc = ($PPL$XMAX1) - 0.06*LK_XRANGE

   ANNOTATE/XPOS=`lk_xlabloc`/YPOS=`lk_y1pt`/HALIGN=1/VALIGN=0/SIZE=($lk_lsize) &quot;($lk_title)&quot;

ELIF `($lk_where%1|ll&gt;1|*&gt;0%) EQ 1` THEN   ! left bottom

   LET lk_x1pt = (0.5*lk_xdel)* lk_xrange + (($XAXIS_MIN))
   LET lk_x2pt = lk_xdel*lk_xrange + lk_x1pt
   
   LET lk_yfac = 0.5*lk_ydel + (($1)-1)* lk_ydel
   LET lk_y1pt = lk_yfac * lk_yrange + ($YAXIS_MIN)
   
   ($plot_command) {`lk_x1pt`,`lk_x2pt`}, {`lk_y1pt`, `lk_y1pt`}

   LET lk_xlabloc = lk_x2pt + 0.02*lk_xrange
   IF ($AX_HORIZ%0|T&gt;1|*&gt;0%) THEN LET lk_xlabloc = ($PPL$XMAX1)+ 0.02*LK_XRANGE
   
   ANNOTATE/XPOS=`lk_xlabloc`/YPOS=`lk_y1pt`/HALIGN=-1/VALIGN=0/SIZE=($lk_lsize) &quot;($lk_title)&quot;

ELIF `($lk_where%1|lr&gt;1|*&gt;0%) EQ 1` THEN   ! right bottom

   LET lk_x1pt = (1-1.5*lk_xdel)* lk_xrange + (($XAXIS_MIN))
   LET lk_x2pt = lk_xdel*lk_xrange + lk_x1pt
   
   LET lk_yfac = 0.5*lk_ydel + (($1)-1)* lk_ydel
   LET lk_y1pt = lk_yfac * lk_yrange + ($YAXIS_MIN)
   

   ($plot_command) {`lk_x1pt`,`lk_x2pt`}, {`lk_y1pt`, `lk_y1pt`}
   
   LET lk_xlabloc = lk_x1pt - 0.02*lk_xrange
   IF ($AX_HORIZ%0|T&gt;1|*&gt;0%) THEN LET lk_xlabloc = ($PPL$XMAX1) - 0.06*LK_XRANGE

   ANNOTATE/XPOS=`lk_xlabloc`/YPOS=`lk_y1pt`/HALIGN=1/VALIGN=0/SIZE=($lk_lsize) &quot;($lk_title)&quot;

ENDIF

! legends in the plot corners.  

! set up to draw outside the plot box
IF `($lk_where%|ulc&gt;1|urc&gt;1|llc&gt;1|lrc&gt;1|*&gt;0%) EQ 1` THEN PPL WINDOW,OFF

IF `($lk_where%1|ulc&gt;1|*&gt;0%) EQ 1` THEN     ! left top

   LET lk_x1pt = (0.5*lk_xdel)* lk_xrange + (($XAXIS_MIN) - 4*lk_xdel*lk_xrange)
   LET lk_x2pt = lk_xdel*lk_xrange + lk_x1pt
   
   LET lk_yfac = (1-0.5*lk_ydel) + (($1)-1)* lk_ydel
   LET lk_y1pt = lk_yfac * lk_yrange + (($YAXIS_MIN) + 2*lk_ydel*lk_yrange)
   
   ($plot_command) {`lk_x1pt`,`lk_x2pt`}, {`lk_y1pt`, `lk_y1pt`}

   LET lk_xlabloc = lk_x2pt + 0.02*lk_xrange
   IF ($AX_HORIZ%0|T&gt;1|*&gt;0%) THEN LET lk_xlabloc = ($PPL$XMAX1)+ 0.02*LK_XRANGE
   
   ANNOTATE/XPOS=`lk_xlabloc`/YPOS=`lk_y1pt`/HALIGN=-1/VALIGN=0/SIZE=($lk_lsize) &quot;($lk_title)&quot;


ELIF `($lk_where%1|urc&gt;1|*&gt;0%) EQ 1` THEN   ! right top

   LET lk_x1pt = (1-1.5*lk_xdel)* lk_xrange + (($XAXIS_MIN) + 4*lk_xdel*lk_xrange)
   LET lk_x2pt = lk_xdel*lk_xrange + lk_x1pt
   
   LET lk_yfac = (1-0.5*lk_ydel) + (($1)-1)* lk_ydel
   LET lk_y1pt = lk_yfac * lk_yrange + (($YAXIS_MIN) + 2*lk_ydel*lk_yrange)

   ($plot_command) {`lk_x1pt`,`lk_x2pt`}, {`lk_y1pt`, `lk_y1pt`}
   
   LET lk_xlabloc = lk_x1pt - 0.02*lk_xrange
   IF ($AX_HORIZ%0|T&gt;1|*&gt;0%) THEN LET lk_xlabloc = ($PPL$XMAX1) - 0.06*LK_XRANGE

   ANNOTATE/XPOS=`lk_xlabloc`/YPOS=`lk_y1pt`/HALIGN=1/VALIGN=0/SIZE=($lk_lsize) &quot;($lk_title)&quot;


ELIF `($lk_where%1|llc&gt;1|*&gt;0%) EQ 1` THEN   ! left bottom

   LET lk_x1pt = (0.5*lk_xdel)* lk_xrange + (($XAXIS_MIN) - 4*lk_xdel*lk_xrange)
   LET lk_x2pt = lk_xdel*lk_xrange + lk_x1pt
   
   LET lk_yfac = 0.5*lk_ydel - (($1)-1)* lk_ydel
   LET lk_y1pt = lk_yfac * lk_yrange + (($YAXIS_MIN) - 2.5*lk_ydel*lk_yrange)
   
   ($plot_command) {`lk_x1pt`,`lk_x2pt`}, {`lk_y1pt`, `lk_y1pt`}

   LET lk_xlabloc = lk_x2pt + 0.02*lk_xrange
   IF ($AX_HORIZ%0|T&gt;1|*&gt;0%) THEN LET lk_xlabloc = ($PPL$XMAX1)+ 0.02*LK_XRANGE
   
   ANNOTATE/XPOS=`lk_xlabloc`/YPOS=`lk_y1pt`/HALIGN=-1/VALIGN=0/SIZE=($lk_lsize) &quot;($lk_title)&quot;


ELIF `($lk_where%1|lrc&gt;1|*&gt;0%) EQ 1` THEN   ! right bottom

   LET lk_x1pt = (1-1.5*lk_xdel)* lk_xrange + (($XAXIS_MIN) + 4*lk_xdel*lk_xrange)
   LET lk_x2pt = lk_xdel*lk_xrange + lk_x1pt
   
   LET lk_yfac = 0.5*lk_ydel - (($1)-1)* lk_ydel
   LET lk_y1pt = lk_yfac * lk_yrange + (($YAXIS_MIN) - 2.5*lk_ydel*lk_yrange)
   

   ($plot_command) {`lk_x1pt`,`lk_x2pt`}, {`lk_y1pt`, `lk_y1pt`}
   
   LET lk_xlabloc = lk_x1pt - 0.02*lk_xrange
   IF ($AX_HORIZ%0|T&gt;1|*&gt;0%) THEN LET lk_xlabloc = ($PPL$XMAX1) - 0.06*LK_XRANGE

   ANNOTATE/XPOS=`lk_xlabloc`/YPOS=`lk_y1pt`/HALIGN=1/VALIGN=0/SIZE=($lk_lsize) &quot;($lk_title)&quot;


ENDIF

ppl window,on
cancel variale lk_*

</pre><pre>! test_linekey.jnl
! Examples using the &quot;linekey.jnl&quot; script for legend lines.


cancel mode logo
go margins 1.4  1.4  1.2  1.2

! Draw an example pair of lines, and show all of the different legend
! locations

plot/i=1:60/thick/sym=20 cos(i/10)
plot/over/nolab/i=1:60/thick/size=0.16/line/sym=30/color=red sin(i/10)

go linekey.jnl 1 &quot;title one&quot; plot/thick/sym=20/color=black ur .12
go linekey.jnl 2 &quot;title two&quot; plot/thick/size=0.16/line/sym=30/color=red ur .12

go linekey.jnl 1 &quot;title one&quot; plot/thick/sym=20/color=black ul .14
go linekey.jnl 2 &quot;title two&quot; plot/thick/size=0.16/line/sym=30/color=red ul .14

go linekey.jnl 1 &quot;title two&quot; plot/thick/size=0.16/line/sym=30/color=red ll .10
go linekey.jnl 2 &quot;title one&quot; plot/thick/sym=20/color=black ll .10

go linekey.jnl 1 &quot;title two&quot; plot/thick/size=0.16/line/sym=30/color=red lr .16
go linekey.jnl 2 &quot;title one&quot; plot/thick/sym=20/color=black lr .16

! Now the legends outside the plot box
go linekey.jnl 1 &quot;title one&quot; plot/thick/sym=20/color=black urc .12
go linekey.jnl 2 &quot;title two&quot; plot/thick/size=0.16/line/sym=30/color=red urc .12

go linekey.jnl 1 &quot;title one&quot; plot/thick/sym=20/color=black ulc .08
go linekey.jnl 2 &quot;title two&quot; plot/thick/size=0.16/line/sym=30/color=red ulc .08

go linekey.jnl 1 &quot;title one&quot; plot/thick/sym=20/color=black llc .18
go linekey.jnl 2 &quot;title two&quot; plot/thick/size=0.16/line/sym=30/color=red llc .18

go linekey.jnl 1 &quot;title one&quot; plot/thick/sym=20/color=black lrc .11
go linekey.jnl 2 &quot;title two&quot; plot/thick/size=0.16/line/sym=30/color=red lrc .11

message now show the example as in test_legend.jnl that used the older legend.jnl script

cancel mode logo

let one=3*x+2
let two=x*x-5
let three=0*x+3

set region /x=-10:10

set viewport ul
go margins 1.4  1.4  1.2  1.2
plot/title=&quot;test plot&quot; one,two,three

go linekey 1 &quot;Variable one&quot; plot/color=black ul
go linekey 2 &quot;Variable two&quot;  plot/color=red ul
go linekey 3 &quot;Variable three&quot; plot/color=green ul


set viewport ur
go margins 1.4  1.4  1.2  1.2
plot/title=&quot;test plot&quot; one,two,three

go linekey 1 &quot;Variable one&quot; plot/color=black ur
go linekey 2 &quot;Variable two&quot;  plot/color=red ur
go linekey 3 &quot;Variable three&quot; plot/color=green ur


set viewport ll
go margins 1.4  1.4  1.2  1.2
plot/title=&quot;test plot&quot; one,two,three

go linekey 1 &quot;Variable one&quot; plot/color=black ll
go linekey 2 &quot;Variable two&quot;  plot/color=red ll
go linekey 3 &quot;Variable three&quot; plot/color=green ll

set viewport lr
go margins 1.4  1.4  1.2  1.2
plot/title=&quot;test plot&quot; one,two,three

go linekey 1 &quot;Variable one&quot; plot/color=black lr
go linekey 2 &quot;Variable two&quot;  plot/color=red lr
go linekey 3 &quot;Variable three&quot; plot/color=green lr

message now draw legends outside the plot boxes.
cancel view

set viewport ul
go margins 1.4  1.4  1.2  1.2
plot/title=&quot;test plot&quot; one,two,three

go linekey 1 &quot;Variable one&quot; plot/color=black ulc
go linekey 2 &quot;Variable two&quot;  plot/color=red ulc
go linekey 3 &quot;Variable three&quot; plot/color=green ulc


set viewport ur
go margins 1.4  1.4  1.2  1.2
plot/title=&quot;test plot&quot; one,two,three

go linekey 1 &quot;Variable one&quot; plot/color=black urc
go linekey 2 &quot;Variable two&quot;  plot/color=red urc
go linekey 3 &quot;Variable three&quot; plot/color=green urc


set viewport ll
go margins 1.4  1.4  1.2  1.2
plot/title=&quot;test plot&quot; one,two,three

go linekey 1 &quot;Variable one&quot; plot/color=black llc
go linekey 2 &quot;Variable two&quot;  plot/color=red llc
go linekey 3 &quot;Variable three&quot; plot/color=green llc

set viewport lr
go margins 1.4  1.4  1.2  1.2
plot/title=&quot;test plot&quot; one,two,three

go linekey 1 &quot;Variable one&quot; plot/color=black lrc
go linekey 2 &quot;Variable two&quot;  plot/color=red lrc
go linekey 3 &quot;Variable three&quot; plot/color=green lrc



! Now tests on time plots
cancel view

use monthly_navy_winds

plot/title=&quot; &quot;/t=1-jan-1982:31-dec-1985/x=180/y=20 uwnd
plot/over/nolab/t=1-jan-1982:31-dec-1985/x=180/y=20/thick/size=0.16/line/sym=30/color=red vwnd

go linekey.jnl 1 &quot;`uwnd,return=title`&quot; plot/thick/sym=20/color=black ur .12
go linekey.jnl 2 &quot;`vwnd,return=title`&quot; plot/thick/size=0.16/line/sym=30/color=red ur .12

go linekey.jnl 1 &quot;`uwnd,return=title`&quot; plot/thick/sym=20/color=black ul .14
go linekey.jnl 2 &quot;`vwnd,return=title`&quot; plot/thick/size=0.16/line/sym=30/color=red ul .14

go linekey.jnl 1 &quot;title two&quot; plot/thick/size=0.16/line/sym=30/color=red ll .10
go linekey.jnl 2 &quot;title one&quot; plot/thick/sym=20/color=black ll .10

go linekey.jnl 1 &quot;title two&quot; plot/thick/size=0.16/line/sym=30/color=red lr .16
go linekey.jnl 2 &quot;title one&quot; plot/thick/sym=20/color=black lr .16

! Now the legends outside the plot box
go linekey.jnl 1 &quot;`uwnd,return=title`&quot; plot/thick/sym=20/color=black urc .12
go linekey.jnl 2 &quot;`vwnd,return=title`&quot; plot/thick/size=0.16/line/sym=30/color=red urc .12

go linekey.jnl 1 &quot;`uwnd,return=title`&quot; plot/thick/sym=20/color=black ulc .08
go linekey.jnl 2 &quot;`vwnd,return=title`&quot; plot/thick/size=0.16/line/sym=30/color=red ulc .08

go linekey.jnl 1 &quot;`uwnd,return=title`&quot; plot/thick/sym=20/color=black llc .18
go linekey.jnl 2 &quot;`vwnd,return=title`&quot; plot/thick/size=0.16/line/sym=30/color=red llc .18

go linekey.jnl 1 &quot;`uwnd,return=title`&quot; plot/thick/sym=20/color=black lrc .11
go linekey.jnl 2 &quot;`vwnd,return=title`&quot; plot/thick/size=0.16/line/sym=30/color=red lrc .11

message now show the example as in test_legend.jnl that used the older legend.jnl script

cancel mode logo


set region /x=-10:10

set viewport ul
go margins 1.4  1.4  1.2  1.2
plot/title=&quot;`uwnd,return=title`&quot;/t=1-jan-1982:31-dec-1985/x=120 uwnd[y=-20], uwnd[y=-0], uwnd[y=20]

go linekey 1 &quot;Y=-20&quot; plot/color=black ul
go linekey 2 &quot;Y=0&quot;  plot/color=red ul
go linekey 3 &quot;Y=20&quot; plot/color=green ul


set viewport ur
go margins 1.4  1.4  1.2  1.2
plot/title=&quot;`uwnd,return=title`&quot;/t=1-jan-1982:31-dec-1985/x=120 uwnd[y=-20], uwnd[y=-0], uwnd[y=20]

go linekey 1 &quot;Y=-20&quot; plot/color=black ur
go linekey 2 &quot;Y=0&quot;  plot/color=red ur
go linekey 3 &quot;Y=20&quot; plot/color=green ur


set viewport ll
go margins 1.4  1.4  1.2  1.2
plot/title=&quot;`uwnd,return=title`&quot;/t=1-jan-1982:31-dec-1985/x=120 uwnd[y=-20], uwnd[y=-0], uwnd[y=20]

go linekey 1 &quot;Y=-20&quot; plot/color=black ll
go linekey 2 &quot;Y=0&quot;  plot/color=red ll
go linekey 3 &quot;Y=20&quot; plot/color=green ll

set viewport lr
go margins 1.4  1.4  1.2  1.2
plot/title=&quot;`uwnd,return=title`&quot;/t=1-jan-1982:31-dec-1985/x=120 uwnd[y=-20], uwnd[y=-0], uwnd[y=20]

go linekey 1 &quot;Y=-20&quot; plot/color=black lr
go linekey 2 &quot;Y=0&quot;  plot/color=red lr
go linekey 3 &quot;Y=20&quot; plot/color=green lr

message now draw legends outside the plot boxes.
cancel view

set viewport ul
go margins 1.4  1.4  1.2  1.2
plot/title=&quot;`uwnd,return=title`&quot;/t=1-jan-1982:31-dec-1985/x=120 uwnd[y=-20], uwnd[y=-0], uwnd[y=20]

go linekey 1 &quot;Y=-20&quot; plot/color=black ulc
go linekey 2 &quot;Y=0&quot;  plot/color=red ulc
go linekey 3 &quot;Y=20&quot; plot/color=green ulc


set viewport ur
go margins 1.4  1.4  1.2  1.2
plot/title=&quot;`uwnd,return=title`&quot;/t=1-jan-1982:31-dec-1985/x=120 uwnd[y=-20], uwnd[y=-0], uwnd[y=20]

go linekey 1 &quot;Y=-20&quot; plot/color=black urc
go linekey 2 &quot;Y=0&quot;  plot/color=red urc
go linekey 3 &quot;Y=20&quot; plot/color=green urc


set viewport ll
go margins 1.4  1.4  1.2  1.2
plot/title=&quot;`uwnd,return=title`&quot;/t=1-jan-1982:31-dec-1985/x=120 uwnd[y=-20], uwnd[y=-0], uwnd[y=20]

go linekey 1 &quot;Y=-20&quot; plot/color=black llc
go linekey 2 &quot;Y=0&quot;  plot/color=red llc
go linekey 3 &quot;Y=20&quot; plot/color=green llc

set viewport lr
go margins 1.4  1.4  1.2  1.2
plot/title=&quot;`uwnd,return=title`&quot;/t=1-jan-1982:31-dec-1985/x=120 uwnd[y=-20], uwnd[y=-0], uwnd[y=20]

go linekey 1 &quot;Y=-20&quot; plot/color=black lrc
go linekey 2 &quot;Y=0&quot;  plot/color=red lrc
go linekey 3 &quot;Y=20&quot; plot/color=green lrc





</pre>
<!--X-Body-of-Message-End-->
<!--X-MsgBody-End-->
<!--X-Follow-Ups-->
<hr>
<!--X-Follow-Ups-End-->
<!--X-References-->
<ul><li><strong>References</strong>:
<ul>
<li><strong><a name="00316" href="msg00316.html">[ferret_users] multiple legend</a></strong>
<ul><li><em>From:</em> Saat Mubarrok</li></ul></li>
</ul></li></ul>
<!--X-References-End-->
<!--X-BotPNI-->
<UL>
<li>Previous by thread:
<strong><a href="msg00316.html">[ferret_users] multiple legend</a></strong>
</li>
<li>Next by thread:
<strong><a href="msg00318.html">Fwd: [ferret_users] multiple legend</a></strong>
</li>

</UL>
<center>[<a href="msg00316.html">Thread Prev</a>][<a href="msg00318.html">Thread Next</a>][<A HREF="threads.html#00317">Index</A>]</center>

<!--X-BotPNI-End-->
<!--X-User-Footer-->
<A HREF="http://ferret.pmel.noaa.gov/Ferret/contact-us">Contact Us</A>
<br>
<font size=-1>
<address>
<a href="http://www.doc.gov">Dept of Commerce</a> /
<a href="http://www.noaa.gov">NOAA</a> /
<a href="http://www.oar.noaa.gov">OAR</a> /
<a href="http://www.pmel.noaa.gov">PMEL</a> /
<a href="http://ferret.pmel.noaa.gov">Ferret</a>
</address>

<br>
 <a href="http://www.noaa.gov/privacy.html">Privacy Policy</a> | <a href="http://www.noaa.gov/disclaimer.html">Disclaimer</a> | <a href="accessibility.html">Accessibility Statement</a> 

</font>
<!--X-User-Footer-End-->
</body>
</html>
