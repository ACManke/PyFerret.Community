<!-- MHonArc v2.5.13 -->
<!--X-Subject: Re: Other vector formats -->
<!--X-From-R13: Oafyrl [naxr <nafyrlNczry.abnn.tbi> -->
<!--X-Date: Tue, 22 Oct 2002 09:25:49 &#45;0700 (PDT) -->
<!--X-Message-Id: 3DB57CE3.D9BAF026@pmel.noaa.gov -->
<!--X-Content-Type: multipart/mixed -->
<!--X-Reference: 02102217094102.01078@meteoro -->
<!--X-Head-End-->
<!doctype html public "-//W3C//DTD HTML//EN">
<html>
<head>
<title>Re: Other vector formats</title>
</head>
<body>
<!--X-Body-Begin-->
<!--X-User-Header-->
<!--X-User-Header-End-->
<!--X-TopPNI-->
<HR>
<center>[<a href="msg00521.html">Thread Prev</a>][<a href="msg00523.html">Thread Next</a>][<A HREF="threads.html#00522">Index</A>]</center>

<!--X-TopPNI-End-->
<!--X-MsgBody-->
<!--X-Subject-Header-Begin-->
<h1>Re: Other vector formats</h1>
<hr>
<!--X-Subject-Header-End-->
<!--X-Head-of-Message-->
<ul>
<li><strong>To</strong>: <strong><a href="mailto:scalabri@DOMAIN.HIDDEN">scalabri@xxxxxxxxxxxxxx</a></strong></li>
<li><strong>Subject</strong>: <strong>Re: Other vector formats</strong></li>
<li><strong>From</strong>: <strong>Ansley Manke &lt;<a href="mailto:ansley@DOMAIN.HIDDEN">ansley@xxxxxxxxxxxxx</a>&gt;</strong></li>
<li>Date: Tue, 22 Oct 2002 09:29:23 -0700</li>
<li>Cc: <a href="mailto:ferret_users@DOMAIN.HIDDEN">ferret_users@xxxxxxxxxxxxxxxxxxx</a></li>
<li>References: &lt;<a href="msg00521.html">02102217094102.01078@meteoro</a>&gt;</li>
<li>Sender: <a href="mailto:owner-ferret_users@DOMAIN.HIDDEN">owner-ferret_users@xxxxxxxxxxxxxxxxxxx</a></li>
</ul>
<!--X-Head-of-Message-End-->
<!--X-Head-Body-Sep-Begin-->
<hr>
<!--X-Head-Body-Sep-End-->
<!--X-Body-of-Message-->
<pre>Hi Salvador,
There is a Ferret script called vector_demo.jnl.  I will attach it;
see if it shows you what you need.   Here are a few more
hints for finding out about a topic:

- Users Guide (in the index under &quot;vector plots&quot;  there are some
  listings)

- The Fgo utility lists Ferret scripts that contain some string
  you think might be helpful.  From the Unix command line
  &gt; Fgo vector
   lists several scripts.  Then inside Ferret you can get information
   on these scripts by typing, for example
   yes? go/help scattered_vectors
   yes? go/help stick_vectors

- FAQ's, in this case not too helpful.

- Users List.  Here it may take some trial and error, for example
   I set the search for &quot;stick vectors&quot;, or &quot;headless vectors&quot;, and got
   some messages that will help.

Ansley Manke

Salvador Calabria Montero wrote:

&gt; Hello,
&gt;
&gt; I would like to draw vector in another format different from the defult way
&gt; Ferret does, that is, what I need is to draw barb vectors: vector without
&gt; head, and with a barb that shows the speed.
&gt;
&gt; I have been looking up in Ferret manual, and in the mail archives, but I have
&gt; not found nothing related yet, though I found a reference in the FAQ section
&gt; (Wind barbs/stick vectors) but with no link to any page.
&gt;
&gt; I wonder if you know to do it if it is possible.
&gt;
&gt; Thank you very much in advance.

--
Ansley Manke  Pacific Marine Environmental Laboratory  Seattle WA  (206)526-6246

</pre>
<pre>\cancel mode verify
! Description: Demonstration of various vector plotting options
! 4/94 *sh*
! updated 9/96 for Ferret V4.4

! *acm* 7/01 update /LINE= to /COLOR=/THICKNESS=

define alias wait message

say **** Welcome to the vector plotting demo ****
say .
say This demo will lead you through a number of options and styles for
say vector plotting in FERRET.  The commands that are producing the
say plots you will see are set off in the style
say
say .   yes? [command]
say
say 
WAIT

! clean up from any previous activity
define region/default save
cancel region
set wind/asp=.75:ax
go white

! ***********************************************************
say ...
say We will begin by examining the global FNOC winds on Jan. 1, 1990
say The FNOC data set (monthly_navy_winds) has variables &quot;uwnd&quot;, and &quot;vwnd&quot;
say We will overlay the continentintal boundaries in red.
say ...

say .    yes? set data monthly_navy_winds
say .    yes? set region/t=1-jan-1990
say .    yes? vector uwnd,vwnd
say .    yes? go land red


set data monthly_navy_winds
set region/t=1-jan-1990
vector uwnd,vwnd
go land red

WAIT

! ***********************************************************
say ...
say Now the same plot done a little nicer ...
say (Pen number 2 is red.)
say ...

say .    yes? go fland 60 red basemap
say .    yes? vector/overlay/length=10/title=&quot;wind speed&quot;  uwnd,vwnd


go fland 60 red basemap
vector/overlay/length=10/title=&quot;wind speed&quot;  uwnd,vwnd


WAIT

! ***********************************************************
say ...
say The command qualifiers /XSKIP and /YSKIP control the thinning of vectors.
say By altering the spacing we can bring out the zonal structure of the winds.
say ...


say .    yes? go fland 60 black basemap
say .    yes? vector/over/len=10/title=&quot;wind speed&quot;/xskip=5/yskip=1/pen=2 uwnd,vwnd

go fland 60 black basemap
vector/overlay/length=10/title=&quot;wind speed&quot;/xskip=5/yskip=1/pen=2 uwnd,vwnd



WAIT

! ***********************************************************
say ...
say In this plot we will see the important effect of the /ASPECT qualifier.
say In this thin slice of the world from latitude 10S to 10N the scaling
say of longitude is very different from latitude.  The /ASPECT qualifier
say rotates the vectors to compensate while preserving their magnitudes.
say ...

 
say .    yes? set view upper
say .    yes? set region/y=10s:10n
say .    yes? go fland 60 red basemap
say .    yes? vector/over/len=10/title=&quot;wind speed&quot;/xskip=1/yskip=1 uwnd,vwnd
say
say .    yes? set view lower
say .    yes? go fland 60 red basemap
say .    yes? vector/over/len=10/title=&quot;wind speed&quot;/xskip=1/yskip=1/aspect uwnd,vwnd


set view upper
set region/y=10s:10n
go magnify 1.5
go fland 60 red basemap
vector/overlay/length=10/title=&quot;wind speed&quot;/xskip=1/yskip=1 uwnd,vwnd

set view lower
go magnify 1.5
go fland 60 red basemap
vector/overlay/length=10/title=&quot;wind speed&quot;/xskip=1/yskip=1/aspect uwnd,vwnd


WAIT

! ***********************************************************
say ...
say Now we will look at the same data but subtly modify the vectors
say by centering them over their coordinate locations.  By default the
say end point of each vector is located at its coordinate point.
say We use the FERRET GO tool &quot;GO centered_vectors&quot;.
say ...


say .    yes? set view upper
say .    yes? go fland 60 red basemap
say .    yes? go centered_vectors vector/overlay/title=&quot;wind speed&quot; uwnd,vwnd 10 


set view upper
go magnify 1.5
go fland 60 red basemap
go centered_vectors vector/overlay/title=&quot;wind speed&quot; uwnd,vwnd 10 
go magnify 1     ! return to normal


WAIT

! ***********************************************************
say ...
say Oceanographers commonly want to look at a line of vectors with a
say &quot;stick&quot; plot.  The GO tool &quot;GO stick_vectors&quot; can provide this.
say ...


say .    yes? set wind/asp=.4/siz=.4
say .    yes? set region/x=180/y=0
say .    yes? set region/t=1-jan-1982:31-jan-1992
say .    yes? GO stick_vectors plot/COLOR=red/title=&quot;FNOC winds&quot; uwnd vwnd


set wind/asp=.4/siz=.4
set region/x=180/y=0
set region/t=1-jan-1982:31-jan-1992
GO stick_vectors plot/COLOR=red/title=&quot;FNOC winds&quot; uwnd vwnd


WAIT
set wind/asp=.75:ax/siz=.6
set region/t=1-jan-1990
sp rm -f vdtmp_*

! ***********************************************************
say ...
say To complete the demonstration we will try a more challenging display.
say We will use FERRETs ability to sub-sample from a data set to generate
say a scatter of vector wind speeds on North Atlantic coasts.
say Then we will use the tool &quot;GO scattered_vectors&quot; to display them.
say
say We will start by displaying a map of the region.
say ...

say .    yes? set region/x=100w:10w/y=0:60     	! North Atlantic
say .    yes? set data etopo60		 	! 60 minute bathymetry data set
say .    yes? FILL/PALETTE=land_sea/TITLE=&quot;Coastal Winds&quot; rose
say .    yes? GO land blue
say
say Now we will create a data file of scattered X,Y,U,V quadruples
say to be plotted.  Use &quot;Fgo -more vector_demo.jnl&quot; to see the FERRET commands

set region/x=100w:10w/y=0:60     		! North Atlantic
set data etopo60		 		! 60 minute bathymetry data set
FILL/PALETTE=land_sea/TITLE=&quot;Coastal Winds&quot;/level=(-10000,0,1000)(0,3000,300) rose

! create a data file of scattered X,Y,U,V quadruples to be plotted
let coastmask = if rose[d=etopo60] ge 0 and rose[d=etopo60] le 20 then 1
user/command=sample/opt2=c/file=vdtmp_u.raw uwnd[d=monthly_navy_winds], x*coastmask, y*coastmask, 1/0, 1/0
user/command=sample/opt2=c/file=vdtmp_v.raw vwnd[d=monthly_navy_winds], x*coastmask, y*coastmask, 1/0, 1/0
sp grep -v &quot;99.9&quot; vdtmp_u.raw &gt; vdtmp_u.dat
sp grep -v &quot;99.9&quot; vdtmp_v.raw &gt; vdtmp_v.dat
file/var=uwnd,xp,yp vdtmp_u.dat
file/var=vwnd,xp,yp vdtmp_v.dat
list/nohead/file=vdtmp_uv.dat/format=(4f10.4)/i=1:999 xp,yp,uwnd[d=vdtmp_u.dat],vwnd[d=vdtmp_v.dat]
sp echo &quot;0 0 0 0&quot; &gt;&gt; vdtmp_uv.dat

say .    yes? ! now overlay the vectors
say .    yes? go scattered_vectors  vdtmp_uv.dat 15 clock
say .    yes? PLOT/VS/D=vdtmp_uv.dat/@aros/OVERLAY/LINE/COLOR=lack/THICK=2/nolab arrow_x,arrow_y

! now overlay the vectors 
go scattered_vectors  vdtmp_uv.dat 15 clock
PLOT/VS/D=vdtmp_uv.dat/@aros/OVERLAY/LINE/COLOR=black/THICKNESS=2/nolabels arrow_x,arrow_y
GO land blue

! clean up
cancel data vdtmp_u.dat, vdtmp_v.dat, vdtmp_uv.dat
sp rm -f vdtmp_*
set region save
set mode/last verify
</pre>

<!--X-Body-of-Message-End-->
<!--X-MsgBody-End-->
<!--X-Follow-Ups-->
<hr>
<!--X-Follow-Ups-End-->
<!--X-References-->
<ul><li><strong>References</strong>:
<ul>
<li><strong><a name="00521" href="msg00521.html">Other vector formats</a></strong>
<ul><li><em>From:</em> Salvador Calabria Montero</li></ul></li>
</ul></li></ul>
<!--X-References-End-->
<!--X-BotPNI-->
<UL>
<li>Previous by thread:
<strong><a href="msg00521.html">Other vector formats</a></strong>
</li>
<li>Next by thread:
<strong><a href="msg00523.html">Re: Other vector formats</a></strong>
</li>

</UL>
<center>[<a href="msg00521.html">Thread Prev</a>][<a href="msg00523.html">Thread Next</a>][<A HREF="threads.html#00522">Index</A>]</center>

<!--X-BotPNI-End-->
<!--X-User-Footer-->
<hr>
<font size=-1>
<address>
<a href="http://www.doc.gov">Dept of Commerce</a> / 
<a href="http://www.noaa.gov">NOAA</a> /
<a href="http://www.oar.noaa.gov">OAR</a> /
<a href="http://www.pmel.noaa.gov">PMEL</a> /
<a href="http://tmap.pmel.noaa.gov">TMAP</a>
</address> <br> <a href="http://ferret.pmel.noaa.gov/Ferret/ferret_contact_us.html">Contact Us</a> | <a href="http://www.noaa.gov/privacy.html">Privacy Policy</a> | <a href="http://www.noaa.gov/disclaimer.html">Disclaimer</a> | <a href="accessibility.html">Accessibility Statement</a> 
</font>
<!--X-User-Footer-End-->
</body>
</html>
